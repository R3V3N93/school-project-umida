class Trash : Actor
{
    DebugInfo debug;
    string sound;
    int timer; // tic
    int stoptimer; //tic
    int gauge;
    int score;
    int velocity;
    Property Weight : gauge;
    Property Score : score;
    Property Speed : velocity;
    Property Sound : sound;
    Handler game;
    Harpoon mouse;
    Default
    {
        Height 10;
        Radius 100;
        Trash.Weight 30;
        Trash.Score 100;
        Trash.Speed 10;
        Trash.Sound "vinyl";
        +NOFRICTION;
        -SOLID;
        +SHOOTABLE;
    }

    override void PostBeginPlay()
    {
        super.PostBeginPlay();
        game = Handler(EventHandler.Find("Handler"));
        mouse = Harpoon(Eventhandler.Find("Harpoon"));
        debug = DebugInfo(Eventhandler.Find("Debuginfo"));
    }

    void accel()
    {
        self.vel.x = -velocity;
    }

    override void Tick()
    {
        super.Tick();
        debug.AddMInfoItem(self.GetClassName()..".timer = "..self.timer);
        self.timer++;

        if(Level.mapname != "TEST")
        {
            if(self.pos.x <= game.screenXLeft)
            {
                game.damagePlayer();
                game.stopCombo();
                self.destroy();
            }

            if(self.vel.x == 0 && game.p.mo.tracer != self)
            {
                /*if(mouses.cursor_actor != self)
                    self.vel.x = -10.0;*/
                stoptimer++;
                if(stoptimer >= 17)
                    self.accel();
            }
            else
                stoptimer = 0;
            
            if(self.gauge <= 0)
            {
                game.grantScore(self.timer, self.score);
                game.doCombo();
                self.destroy();
            }

            if(self.gauge < self.default.gauge)
            {   
                self.gauge++;
                
            }
        }
    }
    States
    {
        Spawn:
            TRSH A 1;
            Loop;
    }
}

class Vinyl : trash
{
    Default
    {
        Trash.Score 50;
        Trash.Weight 12;
        Trash.Speed 12;
        Trash.Sound "vinyl";
    }
}

class PlasticBottle : trash
{
    Default
    {
        Trash.Score 100;
        Trash.Weight 30;
        Trash.Speed 10;
        Trash.Sound "plastic";
    }
}

class Chemical : trash
{
    Default
    {
        Trash.Score 120;
        Trash.Weight 40;
        Trash.Speed 8;
        Trash.Sound "chemical";
    }
}